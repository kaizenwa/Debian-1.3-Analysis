dnl Autoconf configure script for GPC, the GNU Pascal compiler.
dnl Copyright 1995, 1996 Free Software Foundation, Inc.
dnl
dnl This file is part of GPC.
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.10)dnl
AC_INIT(gpc-parse.y)
AC_CONFIG_HEADER(rts/rts-config.h:rts/rts-config.in)

AC_PROG_CC
AC_PROG_MAKE_SET
AC_CANONICAL_HOST
AC_AIX
AC_ISC_POSIX
AC_MINIX

dnl Check for GCC sources and object code.
GPC_PATH_GCCSRC
GPC_PATH_GCCBIN
GPC_GCC_VERSION

dnl Extract configuration from GCC
GPC_GCC_TARGET
GPC_GCC_HOST
GPC_GCC_BUILD

dnl More info from GCC configuration files.
GPC_GCC_PREFIX
GPC_GCC_LOCAL_PREFIX
GPC_GCC_USE_COLLECT2
GPC_GCC_TARGET_DEFAULT
GPC_GCC_EXTRA_OBJS
GPC_GCC_EXTRA_GCC_OBJS
GPC_GCC_OUT_OBJECT_FILE

dnl If we're crosscompiling/crossbuilding, find the apropriate tools.
AR="ar"
RANLIB="ranlib"
if test "${native}" = "no"; then
  GPC_PROG_AR
  GPC_PROG_RANLIB
  GPC_PROG_GCC
fi

dnl Possibly a cross-compiler
GPC_PROG_RTS_GCC

AC_ARG_PROGRAM

dnl RTS configuration

#
# Switch compilers for now: if gpc is configured as a cross-compiler, 
# then configure should check the cross-target's header files.
# 
CC="${RTS_GCC}"

dnl Check for BSD style signals
GPC_BSD_RTS

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/file.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_STRUCT_ST_RDEV
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(ftruncate strcasecmp stricmp setlinebuf gettimeofday strchr strdup memcpy)

#
# Restore ${CC}
#
unset CC
CC="${ac_cv_prog_CC}"

AC_OUTPUT(Makefile rts/Makefile doc/Makefile)

echo "
--------------------------------------------------------------------------
Configuration summary:"

if test "${cross_compiling}" = "yes"; then
  echo "  Building a ${target} crosscompiler hosted by ${host}"
else
  if test "${cross_building}" = "yes"; then
    echo "  Building a ${target} compiler on ${host}"
  else
    echo "  Building a native ${target} compiler"
  fi
fi

echo "
  GPC sources in:         ${srcdir}
  GCC sources in:         ${gccsrc}
  GCC object code in:     ${gccbin}
  GCC version:            ${gcc_version}

  Compiler for GPC:       ${CC}
  Compiler for libgpc.a:  ${RTS_GCC}
  Compiler flags:         ${CFLAGS}
"

if test "${cross_compiling}" = "yes"; then
  echo "  Cross-ar utility:       ${ac_cv_prog_x_ar}"
  echo "  Cross-ranlib utility:   ${ac_cv_prog_x_ranlib}"
else
  if test "${cross_building}" = "yes"; then
    echo "  Cross-ar utility:       ${ac_cv_prog_x_ar}"
    echo "  Cross-ranlib utility:   ${ac_cv_prog_x_ranlib}"
  fi
fi

echo "
  Installation path:      ${prefix}/bin, ${prefix}/lib/gcc-lib/${target}/${gcc_version}
--------------------------------------------------------------------------

Now, type \"make\" to build the compiler and runtime system.
"
