dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.5)
AC_INIT(init.cc)

AC_PROG_INSTALL
AC_PROG_CC
AR=${AR-ar}
AC_SUBST(AR)
AC_PROG_RANLIB
LD=${LD-ld}
AC_SUBST(LD)
DLLTOOL=${DLLTOOL-dlltool}
AC_SUBST(DLLTOOL)

AC_ALLOCA
AC_CANONICAL_SYSTEM
AC_CONFIG_SUBDIRS(glob utils)		
AC_PROG_MAKE_SET

AC_C_CROSS
if test "x$cross_compiling" = "xyes"; then
  if test "x$program_transform_name" = "xs,x,x,"; then
    program_transform_name=""
  fi
  if test "x$program_transform_name" = "x"; then
    program_transform_name="s,^,$target_alias-,"
  else
    program_transform_name="$program_transform_name -e s,^,$target_alias-,"
  fi
fi

dnl If CC can't create a .exe, which can happen when building from scratch
dnl because crt0.o hasn't been installed yet, set up EXE_LDFLAGS to find
dnl newlib.

AC_MSG_CHECKING([if newlib needed])
EXE_LDFLAGS=
AC_TRY_LINK(,
	[/* main already defined */]
	,
	AC_MSG_RESULT(no)
	,
	AC_MSG_RESULT(yes)
	if test -d ../newlib
	then
		EXE_LDFLAGS="-B\$\$rootme/../newlib/ -L.."
	else
		AC_MSG_WARN(newlib not found - utility .exe's may not link)
	fi
)
AC_SUBST(EXE_LDFLAGS)

case "$target_cpu" in
   i386|i486|i586|i686) DLL_ENTRY="_dll_entry@12"
		DEF_DLL_ENTRY="dll_entry@12"
 		ALLOCA="_alloca"
		SYSDEF_DIR="i386"  
		CONFIG_DIR="i386"  ;;
   powerpc*)	DLL_ENTRY="dll_entry" 
		DEF_DLL_ENTRY="dll_entry" 
		ALLOCA=" __allocate_stack"
		SYSDEF_DIR="powerpc" 
		CONFIG_DIR="ppc"  ;;
   *)		AC_MSG_ERROR(Invalid target processor \"$target_cpu\") ;;
esac

AC_SUBST(DLL_ENTRY)
AC_SUBST(DEF_DLL_ENTRY)
AC_SUBST(SYSDEF_DIR)
AC_SUBST(ALLOCA)
AC_SUBST(CONFIG_DIR)
AC_OUTPUT(Makefile cygwin.def:cygwin.din)

