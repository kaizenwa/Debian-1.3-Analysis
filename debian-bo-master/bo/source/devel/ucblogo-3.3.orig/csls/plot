to abs :x
op ifelse :x<0 [-:x] [:x]
end

TO DRAW :X
IF XCOR > :XRANGE [STOP]
SETPOS LIST (XCOR+:DELTAX) (SAFE :YRANGE*FF :MAXHARM)
DRAW :X+:DELTAX
END

TO F :NUM
OP (RADSIN :COEFF*:NUM*:X) / :NUM
END

TO FF :NUM
IF :NUM < 1 [OP 0]
OP (F :NUM)+(FF :NUM-:SKIP)
END

TO KEYWORD :INPUTS :DEFAULTS
SETUP.VALUES :DEFAULTS
IF WORDP :INPUTS [MAKE FIRST :DEFAULTS :INPUTS STOP]
SETUP.VALUES :INPUTS
END

TO PLOT :INPUTS
KEYWORD :INPUTS [MAXHARM 5 DELTAX 3 YRANGE 75 CYCLES 1 XRANGE 230 SKIP 2]
MAKE "COEFF :CYCLES*(RADARCTAN -1 0)/:XRANGE
SS CS HT PU
SETPOS LIST (-:XRANGE) 0
PD
DRAW :DELTAX
END

TO SAFE :Y
IF (ABS :Y) < 0.001 [OP 0]
OP :Y
END

TO SETUP.SIMPLE :LIST
MAKE FIRST :DEFAULTS FIRST :LIST
SETUP.VALUES BF :LIST
END

TO SETUP.VALUES :LIST
IF EMPTYP :LIST [STOP]
IF NUMBERP FIRST :LIST [SETUP.SIMPLE :LIST STOP]
MAKE FIRST :LIST FIRST BF :LIST
SETUP.VALUES BF BF :LIST
END

