dnl Process this file with autoconf to produce a configure script.

AC_INIT(gk.c)

dnl locate tcl dir, read in some stuff from there
AC_ARG_WITH(tcl, [  --with-tcl=DIR          use Tcl 7.5 binaries from DIR],
	TCL_BIN_DIR=$withval, TCL_BIN_DIR=`cd ../tcl7.5/unix; pwd`)
if test ! -d $TCL_BIN_DIR; then
    AC_MSG_ERROR(Tcl directory $TCL_BIN_DIR doesn't exist)
fi

file=$TCL_BIN_DIR/tclConfig.sh
. $file
CC=$TCL_CC
DL_LIBS=$TCL_DL_LIBS
AC_SUBST(CC)
AC_SUBST(DL_LIBS)

dnl Checks for programs.
AC_PROG_RANLIB
AC_PROG_LN_S
AC_PROG_INSTALL

dnl Checks for libraries.
dnl Replace `main' with a function in -lm:
AC_CHECK_FUNC(sin, MATH_LIBS="", MATH_LIBS="-lm")
AC_CHECK_LIB(ieee, main, [MATH_LIBS="-lieee $MATH_LIBS"])
AC_SUBST(MATH_LIBS)

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_HEADER_TIME

AC_PATH_X
if test "$x_includes" != ""; then
	XINCLUDES=-I$x_includes
else
	XINCLUDES="# no special path needed"
fi
AC_SUBST(XINCLUDES)

if test "$x_libraries" = ""; then
	XLIBSW=-lX11
else
	XLIBSW="-L$x_libraries -lX11"
fi
AC_SUBST(XLIBSW)


dnl Checks for library functions.
AC_CHECK_FUNCS(strtol strtoul)

tcl_checkBoth=0
AC_CHECK_FUNC(connect, tcl_checkSocket=0, tcl_checkSocket=1)
if test "$tcl_checkSocket" = 1; then
    AC_CHECK_LIB(socket, main, LIBS="$LIBS -lsocket", tcl_checkBoth=1)
fi
if test "$tcl_checkBoth" = 1; then
    tk_oldLibs=$LIBS
    LIBS="$LIBS -lsocket -lnsl"
    AC_CHECK_FUNC(accept, tcl_checkNsl=0, [LIBS=$tk_oldLibs])
fi
AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, main, [LIBS="$LIBS -lnsl"]))

AC_OUTPUT(Makefile)
