############################################################################
#
#	File:     tempfile.icn
#
#	Subject:  Procedures to produce a temporary file for writing
#
#	Author:   Ralph E. Griswold
#
#	Date:     February 20, 1995
#
############################################################################
#
#  tempfile(prefix, suffix, path, len) produces a "temporary" file that can be
#  written.  The name is chosen so as not to overwrite an existing file.
#  The prefix and suffix are prepended and appended, respectively,
#  to a randomly chosen number.  They default to the empty string.  The
#  path is prepended to the file name; its default is "."  The randomly
#  chosen number is fit into a field of len (default 8) by truncation
#  or right filling with zeros as necessary.
#
#  It is the user's responsibility to remove the file when it is no
#  longer needed.
#
#  tempname(prefix, suffix, path, len) produces the name of a temporary
#  file.
#
############################################################################
#
#  Requires:  UNIX-style path names
#
############################################################################
#
#  See also:  tempname.icn
#
############################################################################
#
#  Links:  exists, randomiz
#
############################################################################

link exists
link randomiz

procedure tempname(prefix, suffix, path, len)
   local name, file

   /prefix := ""
   /suffix := ""
   /path := "."
   prefix := path || "/" || prefix
   /len := 8

   randomize()

   repeat {
      ?1				# change &random
      name := prefix || left(&random, 8, "0") || suffix
      if not exists(name) then return name
      }

end

procedure tempfile(prefix, suffix, path, len)
   local name

   name := tempname(prefix, suffix, path, len)

   return open(name, "w") | fail

end
