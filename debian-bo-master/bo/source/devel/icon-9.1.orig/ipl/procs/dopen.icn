############################################################################
#
#	File:     dopen.icn
#
#	Subject:  Procedures to find/open file on search path
#
#	Author:   Ralph E. Griswold
#
#	Date:     July 15, 1995
#
############################################################################
#
#  These procedures search the current directory, followed by paths
#  on the environment variable DPATH, for the specified file to open.
#  See the standard use of IPATH for ucode files for a model.
#
#  The path separator used here does not work on all platforms.
#  These procedures should be customized for this.
#
#  dopen(s) opens and returns the file s.
#
#  dpath(s) returns the path to s.
#
#  Both fail if the file is not found.
#
############################################################################

procedure dopen(s)		#: open file on DPATH
   local file, paths, path

   if file := open(s) then return file		# look in current directory

   paths := getenv("DPATH") | fail

   s := "/" || s				# platform-specific

   paths ? {
      while path := tab(upto(' ') | 0) do {
         if file := open(path || s) then return file
         tab(many(' ')) | break
         }
      }

   fail

end

procedure dpath(s)		#: full path to file on DPATH
   local file, paths, path, result

   if file := open(s) then return file		# look in current directory

   paths := getenv("DPATH") | fail

   s := "/" || s				# platform-specific

   paths ? {
      while path := tab(upto(' ') | 0) do {
         if file := open(result := path || s) then {
            close(file)
            return result
            }
         tab(many(' ')) | break
         }
      }

   fail

end
