############################################################################
#
#	File:     factors.icn
#
#	Subject:  Procedures for factoring
#
#	Author:   Ralph E. Griswold
#
#	Date:     December 26, 1992
#
############################################################################
#
#  factors(i) returns a list containing the factors of i.
#
#  pfactors(i) returns a list containing the primes that divide i.
#
#  prdecomp(i) returns a list of exponents for the prime decomposition of i
#
############################################################################
#
#  Note:  These procedures are not fast enough for serious computation.
#
############################################################################
#
#  Links: prime
#
############################################################################

link prime

procedure factors(i)
   local facts, p

   facts := []

### needs optimizations

   every p := prime() do {
      while i % p = 0 do {
         put(facts, p)
         i /:= p
         }
      if i = 1 then break
      }

   return facts

end

procedure pfactors(i)
   local facts, p

   facts := []

### needs optimizations

   every p := prime() do {
      if p > i then break
      if i % p = 0 then
         put(facts, p)
      }

   return facts

end

procedure prdecomp(i)
   local decomp, count, p

   decomp := []

### needs optimizations

   every p := prime() do {
      count := 0
      while i % p = 0 do {
         count +:= 1
         i /:= p
         }
      put(decomp, count)
      if i = 1 then break
      }

   return decomp

end
