############################################################################
#
#	File:     mean.icn
#
#	Subject:  Procedures to compute means
#
#	Author:   Gregg M. Townsend and Ralph E. Griswold
#
#	Date:     October 30, 1993
#
############################################################################
#
#  These procedures compute the arithmetic, geometric, and harmonic
#  means of a list of values.  They all fail if there are no values.
#  gmean() and hmean() fail if any value is zero.
#
############################################################################

procedure amean(L[])
   local m

   if *L = 0 then fail

   m := 0.0
   every m +:= !L

   return m / *L

end
   
procedure gmean(L[])
   local m

   if *L = 0 then fail

   m := 1.0
   every m *:= !L
   m := abs(m)
   if m > 0.0 then
      return exp (log(m) / *L)
   else
      fail
end
   
procedure hmean(L[])
   local m, r

   if *L = 0 then fail

   m := 0.0

   every r := !L do {
      if r = 0.0 then fail
      else m +:= 1.0 / r
      }

   return *L / m

end
