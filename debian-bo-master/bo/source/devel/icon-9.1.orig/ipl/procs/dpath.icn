############################################################################
#
#	File:     dopen.icn
#
#	Subject:  Procedure to open file on search path
#
#	Author:   Ralph E. Griswold
#
#	Date:     March 4, 1995
#
############################################################################
#
#  This procedure searches the current directory, followed by paths
#  on the environment variable DPATH, for the specified file.  If it
#  finds it, it returns a full path specification for the file.  Otherwise
#  it fails.
#
#  See the standard use of IPATH for ucode files for a model.
#
#  The path separator used here does not work on all platforms.
#  The procedure should be customized for this.
#
############################################################################

procedure dpath(s)
   local file, paths, path, result

   if file := open(s) then return file		# look in current directory

   paths := getenv("DPATH") | fail

   s := "/" || s				# platform-specific

   paths ? {
      while path := tab(upto(' ') | 0) do {
         if file := open(result := path || s) then {
            close(file)
            return result
            }
         tab(many(' ')) | break
         }
      }

   fail

end
