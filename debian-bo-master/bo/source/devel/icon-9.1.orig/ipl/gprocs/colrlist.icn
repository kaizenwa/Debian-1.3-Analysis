############################################################################
#
#	File:     colrlist.icn
#
#	Subject:  Procedures to produce list of colors
#
#	Author:   Ralph E. Griswold
#
#	Date:     January 10, 1995
#
############################################################################
#
#  colrlist(f) returns a list of the colors given in a file.
#
#  colrplte(p) retuns a list of colors for the palette p.
#
############################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################
#
#  Links:  dopen
#
############################################################################

link dopen

procedure colrlist(f)
   local input, colors, line

   if f === "-" then input := &input
   else (input := dopen(f)) | fail
   colors := []

   while line := read(input) do
      put(colors, ColorValue(line ? tab(upto('\t') | 0)))

   close(input)

   if *colors = 0 then fail

   return colors

end

procedure colrplte(p)
   local colors

   colors := []
 
   every put(colors, PaletteColor(p, !PaletteChars(p)))

   if *colors = 0 then fail			# invalid palette

   return colors


end
