############################################################################
#
#	File:     linddraw.icn
#
#	Subject:  Procedure to draw L-System strings
#
#	Author:   Ralph E. Griswold
#
#	Date:     July 7, 1993
#
############################################################################
#
#  This procedure draws strings of characters produced by
#  L-Systems.
#
############################################################################
#
#  Links:  lindgen, turtle
#
############################################################################

link lindgen
link turtle

#  The drawing is based on the axiom and the rewriting rules.  The other
#  parameters are the line length, the angle delta between lines, and
#  the number of generations.  Drawing starts at x,y.

procedure linddraw(x, y, axiom, rewrite, length, delta, gener)
   local c

   /x := (WAttrib(\&window, "width") / 2) | 250
   /y := (WAttrib(\&window, "height") / 2) | 250
   /length := 5
   /delta := 90

   TReset()
   TGoto(x, y)

   every c := lindgen(!axiom, rewrite, gener) do
      case c of {
         "F":   TDraw(length)			# draw forward
         "f":   TSkip(length)			# skip forward
         "+":   TRight(delta)			# turn right
         "-":   TLeft(delta)			# turn left
         "[":   TSave()				# save state
         "]":   TRestore()			# restore state
         }					# ignore other characters

   WFlush()

   return

end
