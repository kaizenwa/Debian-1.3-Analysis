###########################################################################
#
#	File:     splat.icn
#
#	Subject:  Program to draw random circles
#
#	Author:   Jon Lipp
#
#	Date:     June 22, 1991
#
###########################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################
#
#  Links:  color, vidgets, vradio, wopen
#
############################################################################

link color, vidgets, vradio, wopen

global COLORS, state, region, Go, Stop, Quit, Redraw

procedure main (arg)
   local win, root, rb

   Go := "GO"; Stop := "STOP"; Quit := "QUIT"; Redraw := "CLEAR"
   COLORS := ["plum", "slate blue", "olive drab", "coral", "wheat"]

   win := WOpen("label=splat", "size=500,500") |
      stop("*** cannot open window")
   root  := Vroot_frame(win)
   rb := Vradio_buttons(root, 10, 10, 
      win, ["GO", "STOP", "CLEAR", "QUIT"], state_change)
   region := Vpane(win, , , 2)
   VInsert(root, region, 80, 10, 400, 400)

   VSet(rb, "STOP")
   VResize(root)

   state := Stop
   repeat {
      while (*Pending(win) > 0) | (state == Stop) | (state == Redraw) do
         VEvent(root, Event(root.win), &x, &y)
      Splat(region)
      }
end

procedure state_change(vid, val)
   state := val
   if state == Quit then exit()
   if state == Redraw then 
      EraseArea(region.win, region.ax + 1, region.ay + 1, region.aw - 2,
         region.ah - 2)
end

procedure Splat(box)
   local x, y, w, h, c, y3
   
   h := w := ?100
   x := ?(box.aw - w)
   y := ?(box.ah - h)
   y3 := box.ay + ?(box.ah)
   c := ?5
   Shade(box.win, COLORS[c])
   FillArc(box.win, box.ax + x, box.ay + y, w, h)
end

