############################################################################
#
#	File:     colormap.icn
#
#	Subject:  Program to display palette from color list
#
#	Author:   Ralph E. Griswold
#
#	Date:     January 20, 1995
#
############################################################################
#
#  This program shows the colors given in a list from standard input.
#
#  The name is inappropriate, but "palette" already was taken.
#
#  The following options are supported:
#
#	-f s	file containing list of colors
#	-c i	number of color cells in a row, default 16
#	-w i	width of color cell, default 20
#
#  colormap will display color lists with more than 256 entries but,
#  of course, it cannot display more than different colors.
#
############################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################
#
#  Links:  colrlist, dopen, options, wopen
#
############################################################################

$define CellWidth 20
$define Cells 16

link colrlist
link dopen
link options
link wopen

procedure main(args)
   local i, j, clist, depth, opts, width, cells, file

   opts := options(args, "f:c+w+")
   width := \opts["w"] | CellWidth
   cells := \opts["c"] | Cells
   file := \opts["f"] | "-"

   clist := colrlist(file) | stop("*** cannot open color list")

   depth := *clist / cells
   if *clist % cells ~= 0 then depth +:= 1

   WOpen("size=" || cells * width || "," || depth * width, "bg=black") |
      stop("*** cannot open window")

   every j := 0 to depth -1 do
      every i := 0 to depth - 1 do {
         Fg(get(clist)) | break
         FillRectangle(i * width + 1, j * width + 1, width - 1,
            width - 1)
         }

   Event()

end
