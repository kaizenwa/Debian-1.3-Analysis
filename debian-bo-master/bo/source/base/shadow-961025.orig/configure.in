dnl Process this file with autoconf to produce a configure script.
AC_INIT(lib/dialchk.c)
AC_CONFIG_HEADER(config.h)

dnl Automake doc recommends to do this only here.
PACKAGE=shadow
VERSION=961025
PACKAGE_VERSION="$PACKAGE $VERSION"
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(PACKAGE_VERSION)

dnl Some hacks...
test "$prefix" = "NONE" && prefix="/usr"
test "$prefix" = "/usr" && exec_prefix=""
test "$CFLAGS" = "" && CFLAGS="-O2 -Wall"
test "$LDFLAGS" = "" && LDFLAGS="-s"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_ARG_PROGRAM

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h sys/time.h utmp.h utmpx.h)
AC_CHECK_HEADERS(termios.h termio.h sgtty.h sys/ioctl.h syslog.h)
AC_CHECK_HEADERS(paths.h usersec.h utime.h ulimit.h sys/resource.h)
AC_CHECK_HEADERS(gshadow.h shadow.h lastlog.h)

AC_EGREP_HEADER(ut_host, utmp.h, AC_DEFINE(UT_HOST))
AC_EGREP_HEADER(ut_name, utmp.h, AC_DEFINE(UT_USER, ut_name))
AC_EGREP_HEADER(ll_host, lastlog.h, AC_DEFINE(HAVE_LL_HOST))

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_STRUCT_ST_RDEV
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_GETGROUPS
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_FUNC_UTIME_NULL
AC_CHECK_FUNCS(gethostname gettimeofday getusershell strcspn strspn strtol)
AC_CHECK_FUNCS(strftime strptime)
AC_CHECK_FUNCS(initgroups getgroups setgroups sigaction getutent getspnam)

AC_REPLACE_FUNCS(mkdir rename rmdir strdup strstr)
AC_REPLACE_FUNCS(putgrent putpwent putspent sgetgrent sgetpwent sgetspent)

AC_CACHE_CHECK(for pw_age in struct passwd,
ac_cv_struct_passwd_pw_age, AC_TRY_COMPILE([#include <pwd.h>],
[ struct passwd pw;  pw.pw_age = "" ],
ac_cv_struct_passwd_pw_age=yes, ac_cv_struct_passwd_pw_age=no))

if test "$ac_cv_struct_passwd_pw_age" = "yes"; then
	AC_DEFINE(ATT_AGE)
fi

AC_CACHE_CHECK(for pw_comment in struct passwd,
ac_cv_struct_passwd_pw_comment, AC_TRY_COMPILE([#include <pwd.h>],
[ struct passwd pw;  pw.pw_comment = "" ],
ac_cv_struct_passwd_pw_comment=yes, ac_cv_struct_passwd_pw_comment=no))

if test "$ac_cv_struct_passwd_pw_comment" = "yes"; then
	AC_DEFINE(ATT_COMMENT)
fi

AC_CACHE_CHECK(for pw_quota in struct passwd,
ac_cv_struct_passwd_pw_quota, AC_TRY_COMPILE([#include <pwd.h>],
[ struct passwd pw;  pw.pw_quota = "" ],
ac_cv_struct_passwd_pw_quota=yes, ac_cv_struct_passwd_pw_quota=no))

if test "$ac_cv_struct_passwd_pw_quota" = "yes"; then
	AC_DEFINE(BSD_QUOTA)
fi

if test "$ac_cv_header_shadow_h" = "yes"; then
AC_CACHE_CHECK(for working shadow group support,
ac_cv_libc_shadowgrp, AC_TRY_RUN(
[
#include <shadow.h>
main()
{
	struct sgrp *sg = sgetsgent("test:x::");
	/* NYS libc on Red Hat 3.0.3 has broken shadow group support */
	return !sg || !sg->sg_adm || !sg->sg_mem;
}
],
ac_cv_libc_shadowgrp=yes,ac_cv_libc_shadowgrp=no,ac_cv_libc_shadowgrp=no))

if test "$ac_cv_libc_shadowgrp" = "yes"; then
	AC_DEFINE(HAVE_SHADOWGRP)
fi
fi

AC_MSG_CHECKING(location of mail spool files)
for dir in /var/spool/mail /var/mail /usr/spool/mail /usr/mail NONE; do
	if test "$dir" = "NONE"; then
		AC_MSG_WARN(mail spool directory not found)
	elif test -d $dir; then
		AC_DEFINE_UNQUOTED(MAIL_SPOOL_DIR, "$dir")
		AC_MSG_RESULT($dir)
		break
	fi
done

AC_MSG_CHECKING(location of utmp)
for utmp in /var/run/utmp /var/adm/utmp /usr/adm/utmp /etc/utmp NONE; do
	if test "$utmp" = "NONE"; then
		AC_DEFINE_UNQUOTED(_UTMP_FILE, "/etc/utmp")
		AC_MSG_RESULT(not found, assuming /etc/utmp)
	elif test -f $utmp; then
		AC_DEFINE_UNQUOTED(_UTMP_FILE, "$utmp")
		AC_MSG_RESULT($utmp)
		break
	fi
done

AC_MSG_CHECKING(location of wtmp)
for wtmp in /var/log/wtmp /var/adm/wtmp /usr/adm/wtmp /etc/wtmp NONE; do
	if test "$wtmp" = "NONE"; then
		AC_DEFINE_UNQUOTED(_WTMP_FILE, "/var/adm/wtmp")
		AC_MSG_RESULT(not found, assuming /var/adm/wtmp)
	elif test -f $wtmp; then
		AC_DEFINE_UNQUOTED(_WTMP_FILE, "$wtmp")
		AC_MSG_RESULT($wtmp)
		break
	fi
done

AC_MSG_CHECKING(location of lastlog)
for llog in /var/log/lastlog /var/adm/lastlog /usr/adm/lastlog NONE; do
	if test "$llog" = "NONE"; then
		AC_MSG_RESULT(not found)
	elif test -f $llog; then
		AC_DEFINE_UNQUOTED(LASTLOG_FILE, "$llog")
		AC_MSG_RESULT($llog)
		break
	fi
done


dnl XXX - quick hack, should disappear before anyone notices :).
AC_DEFINE(SHADOWPWD)
AC_DEFINE(USG)
AC_DEFINE(AGING)
AC_DEFINE(USE_SYSLOG)
AC_DEFINE(RLOGIN)
AC_DEFINE(RUSEROK,0)
AC_DEFINE(NEED_AL64)
AC_DEFINE(NDEBUG)
AC_DEFINE(CHFN_PROGRAM,"/usr/bin/chfn")
AC_DEFINE(CHSH_PROGRAM,"/usr/bin/chsh")
AC_DEFINE(GPASSWD_PROGRAM,"/usr/bin/gpasswd")
AC_DEFINE(PASSWD_PROGRAM,"/usr/bin/passwd")
AC_DEFINE(LOGIN_ACCESS)
AC_DEFINE(SU_ACCESS)
AC_DEFINE(FAILLOG_LOCKTIME)
AC_DEFINE(CONSOLE_GROUPS)

AC_ARG_ENABLE(shadowgrp, [  --enable-shadowgrp      enable shadow group support], AC_DEFINE(SHADOWGRP))

AC_ARG_WITH(libcrypt, [  --with-libcrypt         try to use libcrypt (default if found)])
AC_ARG_WITH(libcrack, [  --with-libcrack         try to use libcrack (default if found)])
AC_ARG_WITH(md5crypt, [  --with-md5crypt         include code to generate md5 passwords])
AC_ARG_WITH(libskey, [  --with-libskey          use libskey for S/Key support])

AC_SUBST(LIBCRYPT)
AC_SUBST(CRYPTOBJS)
CRYPTOBJS="md5.o md5crypt.o"
if test "$with_libcrypt" = "no"
then
	:
else
	AC_CHECK_LIB(crypt, crypt, AC_DEFINE(HAVE_LIBCRYPT) LIBCRYPT=-lcrypt CRYPTOBJS= )
fi

AC_SUBST(LIBCRACK)
if test "$with_libcrack" = "no"
then
	:
else
	echo "checking cracklib flavour, don't be surprised by the results"
	AC_CHECK_LIB(crack, FascistCheck, AC_DEFINE(HAVE_LIBCRACK) LIBCRACK=-lcrack)
	AC_CHECK_LIB(crack, FascistHistory, AC_DEFINE(HAVE_LIBCRACK_HIST))
	AC_CHECK_LIB(crack, FascistHistoryPw, AC_DEFINE(HAVE_LIBCRACK_PW))
fi

if test "$with_md5crypt" = "no"
then
	:
else
	AC_DEFINE(MD5_CRYPT)
fi

AC_SUBST(LIBSKEY)
if test "$with_libskey" = "yes"
then
	AC_CHECK_LIB(skey, skeychallenge, AC_DEFINE(SKEY) LIBSKEY=-lskey)
fi

AC_OUTPUT(libmisc/Makefile man/Makefile lib/Makefile src/Makefile Makefile
	contrib/Makefile doc/Makefile etc/Makefile old/Makefile shlib/Makefile,
	echo timestamp > stamp-h)
