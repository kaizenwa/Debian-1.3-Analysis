dnl Process this file with autoconf to produce a configure script.
dnl $Id: configure.in,v 1.9 1996/11/21 21:48:02 src Exp $
dnl copyright 1996 Egil Kvaleberg, egilk@sn.no
AC_INIT(common.h)
AC_CONFIG_HEADER(config.h)

VERSION=0.1

AC_REVISION(Configure for NewsX $VERSION)
DATE1=`date | colrm 1 8 | colrm 3`
DATE2=`date | colrm 1 4 | colrm 4`
DATE3=`date | colrm 1 24`
VERDATE="$DATE1 $DATE2 $DATE3"
AC_SUBST(VERSION)
AC_SUBST(VERDATE)

AC_MSG_CHECKING(for a news spool)
list="/var/spool/news /usr/spool/news /var/news"
NEWSSPOOL=
if test x$NEWSSPOOL = x ; then
  for a in ${list} ; do
    if test -d $a ; then
      NEWSSPOOL=$a
      AC_MSG_RESULT($a)
      break
    fi
  done
fi
if test x$NEWSSPOOL = x ; then
   NEWSSPOOL=/var/spool/news
   AC_MSG_RESULT(WARNING: not found, default to $NEWSSPOOL)
fi
AC_SUBST(NEWSSPOOL)

AC_MSG_CHECKING(for a news active file)
list="/usr/lib/news /usr/spool/news /usr/share/news /var/db/news 
 /usr/local/news/control /local/news /usr/local/lib/news"
NEWSCTL=
for a in ${list} ; do
  dnl The "active" file is the common denominator for the news systems
  if test -f $a/active ; then
      NEWSCTL=$a
      AC_MSG_RESULT($a)
      break
  fi
done
if test x$NEWSCTL = x ; then
  NEWSCTL=/usr/lib/news
  AC_MSG_RESULT(WARNING: not found, default to $NEWSCTL)
fi
AC_SUBST(NEWSCTL)

AC_MSG_CHECKING(for Inn or Cnews default)
if test -f $a/newsfeeds; then
  INN_MODE=1
  AC_MSG_RESULT(Inn)
else 
  if test -f $a/sys; then
    INN_MODE=0
    AC_MSG_RESULT(Cnews)
  else
    INN_MODE=0
    AC_MSG_RESULT(WARNING: not found, default to Cnews)
  fi
fi
AC_SUBST(INN_MODE)

dnl BUG: Check for news user
NEWSUSER=news
AC_SUBST(NEWSUSER)

dnl Checks for programs.
AC_PROG_CC

dnl Checks for libraries.
dnl BUG: the following should have been implemented better..
dnl NOTE: uncomment these if you don't want to use the built in dbz:
dnl AC_CHECK_LIB(dbz, dbminit)
dnl AC_CHECK_LIB(dbm, dbminit)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(sys/time.h limits.h syslog.h unistd.h values.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(socket strerror)

AC_OUTPUT(README Makefile, [
# Makefile uses this timestamp file to record whether config.h is up to date.
touch stamp-config])
