#!/bin/sh
# create a header file for xcolorsel.c to compile in the choosen Application defaults
# No I don't wanna to copyright this trash.
# First version Sat Sep  4 18:04:35 GMT 1993, Michael Weller

sed '
#kill comments
/^\!/d
#and preprocessor commands
/^\#/d
#and empty or blank lines (There is a Tab and a blank between the brackets!)
/^[ 	]*$/d
#escape embedded \
s/\\/\\\\/g
#escape embedded "
s/"/\\"/g
#insert leading "
s/^/"/
#append ",
s/$/",/
#if it was a continued line erase the , and let ANSI C do the magic:
s/\\\\",/"/' $*
