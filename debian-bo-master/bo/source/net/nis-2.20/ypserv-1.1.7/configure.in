#
# $Id: configure.in,v 1.12 1997/04/08 21:15:27 kukuk Exp $
#
dnl Process this file with autoconf to produce a configure script.
AC_INIT(server.c)
AC_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr)
AC_PREFIX_PROGRAM(ypserv)

dnl We want autoconf to check whether -g is available
dnl We reset it back soon.
CCOPTS="$CFLAGS"
unset CFLAGS

dnl This sets/resets compiling with -g by default. It should be set to yes for
dnl development versions and set to no for release versions.
use_cc_g_flag=no

dnl Checks for programs.
AC_PROG_CC
# If we're using gcc, we want warning flags
test -n "$GCC" &&
  WARNFLAGS="-Wall"
AC_SUBST(WARNFLAGS)

dnl
dnl This part supplies reasonable defaults for CFLAGS, if they weren't
dnl specified by ''CFLAGS=flags ./configure''
dnl
if test "x$CCOPTS" = x; then
    if test x$GCC = xyes; then
        CCOPTS='-O2'
    else
	CCOPTS="$CFLAGS"
    fi
fi
CFLAGS="$CCOPTS"

dnl Ultrix is nasty. A number of functions e.g. xdr_yppushresp_xfr
dnl clashes with Ultrix libc sometimes. Tried making a testcase, but
dnl that linked fine. Only thing left is checking explicitly for ULTRIX.
AC_MSG_CHECKING(for ULTRIX)
if test `uname` = ULTRIX
then
   AC_MSG_RESULT(yes, sigh)
   AC_DEFINE(ULTRIX_KLUDGE)
else
   AC_MSG_RESULT(no)
fi
AC_MSG_CHECKING(for OSF1)
if test `uname` = OSF1
then
   AC_MSG_RESULT(yes)
   AC_DEFINE(OSF_KLUDGE)
else
   AC_MSG_RESULT(no)
fi

dnl Check for big or little endian system
AC_C_BIGENDIAN

AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Search a awk program
AC_PATH_PROGS(AWK, mawk gawk nawk awk, ERROR)
if test $AWK = ERROR
then
   echo " "
   echo "ERROR: You must have at last one of the following programs:"
   echo "       mawk, gawk, nawk or awk !"
   echo " "
   exit
fi

dnl Search rm
AC_PATH_PROG(RM, rm)
if test x$RM = x
then
   echo " "
   echo "Warning: No rm on this system?"
   echo " "
   exit
fi
AC_SUBST(RM)

dnl Search cat
AC_PATH_PROG(CAT, cat)
if test x$CAT = x
then
   echo " "
   echo "Warning: No cat on this system?"
   echo " "
   exit
fi
AC_SUBST(CAT)

dnl Should we install ypmake? default no
AC_ARG_ENABLE(ypmake,
	[  --enable-ypmake                  Install and use ypmake [default=no]],
              YPMAKE=$enableval, YPMAKE=no)
AC_SUBST(YPMAKE)

dnl Checks for libraries.
AC_ARG_ENABLE(tcp-wrapper,
	[  --enable-tcp-wrapper[=BASE-DIR]  Specifies the use of tcp wrapper [default=no]],
	if test x$enableval != xno
	then
	   if test x$enableval = xyes
	   then
	      AC_CHECK_LIB(wrap, main, TCP_WRAP="-lwrap", TCP_WRAP="")
	      AC_DEFINE(HAVE_LIBWRAP)
           else
	      TCP_WRAP="-L$enableval/lib -lwrap"
	      AC_DEFINE(HAVE_LIBWRAP)
	      CPPFLAGS="$CPPFLAGS -I$enableval/include"
	   fi
	fi
)
AC_SUBST(TCP_WRAP)

libgdbm_parameter=no
AC_ARG_WITH(gdbm-dir, 
        [  --with-gdbm-dir=BASE-DIR         Specifies the base gdbm directory],
        if test x$withval = xyes
        then 
                AC_MSG_WARN(Usage is: --with-gdbm-dir=base-dir)
        else
                LIBDBM="-L$withval/lib -lgdbm"
                AC_DEFINE(HAVE_LIBGDBM)
                CPPFLAGS="$CPPFLAGS -I$withval/include"
		libgdbm_parameter=yes                
        fi
)
AC_SUBST(LIBDBM)

if test x$libgdbm_parameter != xyes
then
AC_CHECK_LIB(gdbm,gdbm_open,LIBDBM="-lgdbm",LIBDBM="")
AC_DEFINE(HAVE_LIBGDBM)
fi

if test x"" = x"${LIBDBM}"
then
  echo "

 You need the GNU GDBM library for this package !"
  echo ""
  echo ""
  exit
fi

AC_CHECK_LIB(nsl,gethostbyname)
AC_CHECK_FUNCS(getrpcport)
if eval "test \"`echo '$ac_cv_func_getrpcport'`\" = yes"; then
	LIBRPC=""
else
	AC_CHECK_LIB(rpcsvc,getrpcport,LIBRPC="-lrpcsvc",LIBRPC="")
fi
AC_SUBST(LIBRPC)


AC_CHECK_LIB(socket,socket)
AC_CHECK_LIB(resolv, res_gethostbyname, RESOLV="-lresolv", RESOLV="")
if test x$RESOLV != x
then
	AC_DEFINE(HAVE_RES_GETHOSTBYNAME)
else
	AC_CHECK_LIB(resolv+, gethostbyname, RESOLV="-lresolv+", RESOLV="")
fi
AC_SUBST(RESOLV)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h paths.h sys/file.h sys/time.h syslog.h unistd.h)
AC_CHECK_HEADERS(getopt.h rpc/clnt_soc.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME

dnl Checks if resultproc_t has been defined
AC_MSG_CHECKING(for resultproc_t in rpc/clnt.h)
AC_EGREP_HEADER(resultproc_t,rpc/clnt.h,\
	AC_DEFINE(HAVE_RESULTPROC_T) AC_MSG_RESULT(yes),\
	AC_MSG_RESULT(no))
AC_MSG_CHECKING(for resultproc_t in rpc/pmap_clnt.h)
AC_EGREP_HEADER(resultproc_t,rpc/pmap_clnt.h,\
	AC_DEFINE(HAVE_RESULTPROC_T) AC_MSG_RESULT(yes),\
	AC_MSG_RESULT(no))

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_FUNC_VPRINTF
AC_FUNC_WAIT3
AC_CHECK_FUNCS(wait4 inet_aton vsyslog)
AC_CHECK_FUNCS(strdup strerror strstr snprintf)
AC_CHECK_FUNCS(_dns_gethostbyname)
AC_CHECK_FUNCS(gethostname gettimeofday select socket uname getopt_long)
AC_CHECK_FUNCS(_rpc_dtablesize getdtablesize)

AC_OUTPUT(Makefile)

echo "
Configuration:

  Source code location:       ${srcdir}
  Compiler:                   ${CC}
  Compiler flags:             ${CFLAGS}
  Preprocessor:		      ${CPP}
  Preprocessor flags:         ${CPPFLAGS}
  Libraries:		      ${LIBS} ${TCP_WRAP} ${LIBDBM} ${LIBRPC}
  Awk:                        ${AWK}
  rm:                         ${RM}
  cat:                        ${CAT}
  Install path prefix:        ${prefix}"
if test ${sysconfdir} != '${prefix}/etc'
then
  echo "  Install ypserv.conf in:     ${sysconfdir}"
else
  echo "  Install ypserv.conf in:     ${prefix}/etc"
fi
  echo "  Install ypmake:             ${YPMAKE}"
echo ""
