dnl Process this file with autoconf to produce a configure script.
AC_INIT(guile.c)
AM_INIT_GUILE_MODULE(guile)

plugdirs=
for dir in `cd $srcdir/.. && echo */PLUGIN`; do
   test -d $srcdir/../$dir && plugdirs="$plugdirs $dir"
done

AC_PROG_CC
AC_PROG_CPP

#----------------------------------------------------------------
#
# Get the plug-ins Makefile tweaks:
#
#----------------------------------------------------------------

xtra_cflags=
xtra_libs=
xtra_dependencies=

for pdir in $plugdirs; do
   file="$srcdir/../$pdir/guile.config"
   if test -f "${file}"; then
      . "${file}"
   else
      file="../$pdir/guile.config"
      if test -f "${file}"; then
	 . "${file}"
      fi
   fi
done

#----------------------------------------------------------------
#
# Threads configuration magic (should probably be fixed)
#
#----------------------------------------------------------------

CY_AC_WITH_THREADS

xtra_cflags="$cy_cv_threads_cflags $xtra_cflags"
xtra_libs="$cy_cv_threads_libs $xtra_libs"

# try adding $X_LIBS to make sure we have the -L for the X library
AC_PATH_X
AC_PATH_XTRA
AC_CHECK_LIB(X11, main)
xtra_libs="$X_LIBS $xtra_libs"

#----------------------------------------------------------------
#
# Get the plug-in initialization routines:
#
#----------------------------------------------------------------

files=''
for pdir in $plugdirs; do
   file="$srcdir/../$pdir/guile.inits"
   test -f "${file}" && files="${files} ${file}"
   file="../$pdir/guile.inits"
   test -f "${file}" && files="${files} ${file}"
done

# FIXME: tsort is not in GNU standards.
# FIXME: doing things this way means initplugs.c cannot be
# regenerated by "make".
cat ${files} /dev/null | tsort | sed -e "s/$/();/" > initplugs.c


#----------------------------------------------------------------
#
# Get the plug-in libraries in the order they have to be 
# linked against:
#
#----------------------------------------------------------------

files=''
for pdir in $plugdirs; do
   file="$srcdir/../$pdir/guile.libs"
   test -f "${file}" && files="${files} ${file}"
   file="../$pdir/guile.libs"
   test -f "${file}" && files="${files} ${file}"
done

# FIXME: tsort, xargs not in GNU standards.
plugin_libs=`cat ${files} /dev/null | tsort | xargs echo`

AC_CHECK_LIB(socket,main)
AC_CHECK_LIB(nsl,main)

AC_DEFINE_UNQUOTED(GUILE_VERSION, "$GUILE_VERSION")

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(xtra_cflags)
AC_SUBST(xtra_libs)
AC_SUBST(xtra_dependencies)
AC_SUBST(plugin_libs)
AC_SUBST(GUILE_MAJOR_VERSION)
AC_SUBST(GUILE_MINOR_VERSION)
AC_SUBST(GUILE_VERSION)
AC_OUTPUT(Makefile)
